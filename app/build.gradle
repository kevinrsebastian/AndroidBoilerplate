plugins {
    id "com.android.application"
    id "dagger.hilt.android.plugin"
    id "kotlin-android"
    id "kotlin-kapt"
    id 'kotlin-parcelize'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.kevinrsebastian.androidboilerplate"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.kevinrsebastian.androidboilerplate.CustomTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding true
    }

    sourceSets {
        androidTest.java.srcDir "src/test/instrumentation/java"
        androidTest.java.srcDir "src/test/common/java"
        test.java.srcDir "src/test/common/java"
        test.java.srcDir "src/test/unit/java"
    }
}

dependencies {
    /* AndroidX Support Commons */
    // https://developer.android.com/jetpack/androidx/versions
    implementation "androidx.appcompat:appcompat:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.fragment:fragment-ktx:1.4.0"

    /* AndroidX Lifecycle */
    // https://developer.android.com/jetpack/androidx/releases/lifecycle#groovy
    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    /* Hilt DI */
    // https://developer.android.com/training/dependency-injection/hilt-android
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    // For Testing
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"

    /* Material Design */
    // https://material.io/develop/android/docs/getting-started
    implementation "com.google.android.material:material:1.4.0"

    /* OkHttp Logging and MockWebServer */
    // https://github.com/square/okhttp/tree/master/okhttp-logging-interceptor
    // https://github.com/square/okhttp/tree/master/mockwebserver
    def okhttp_version = "4.9.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    /* ReactiveX */
    // https://reactivex.io
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "io.reactivex.rxjava3:rxjava:3.1.3"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"

    /* Retrofit */
    // https://github.com/square/retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    /* Room DB */
    // https://developer.android.com/reference/androidx/room/package-summary
    // https://developer.android.com/training/data-storage/room
    def room_version = "2.4.1"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    /* Timber */
    // https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    /** Test Dependencies **/

    /* AndroidX Test */
    def espresso_version = "3.4.0"
    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation("androidx.test.espresso:espresso-core:$espresso_version")
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espresso_version")
    androidTestImplementation("androidx.test.espresso:espresso-intents:$espresso_version")
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"

    /* AssertJ */
    def assertj_version = "3.22.0"
    androidTestImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"

    /* DataFactory */
    def datafactory_version = "0.8"
    androidTestImplementation "org.fluttercode.datafactory:datafactory:$datafactory_version"
    testImplementation "org.fluttercode.datafactory:datafactory:$datafactory_version"

    /* JUnit */
    testImplementation "junit:junit:4.13.2"

    /* Matchers */
    // http://hamcrest.org/JavaHamcrest/distributables
    androidTestImplementation 'org.hamcrest:hamcrest:2.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'

    /* Mockito */
    def mockito_version = "4.2.0"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
}
